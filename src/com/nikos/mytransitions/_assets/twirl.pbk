<languageVersion : 1.0;>

kernel NewFilter
<   namespace : "NikosPBK";
    vendor : "Nikos M.";
    version : 1;
    description : "Twirl Map";
>
{
    input image4 src;
    output pixel4 dst;
    
    parameter float2 center
    <
        defaultValue : float2(256.0, 256.0);
        minValue : float2(0.0,0.0);
        maxValue : float2(1024.0,1024.0);        
    >;

    parameter float radius
    <
        defaultValue : float(256.0);
        minValue : float(0.0);
        maxValue : float(1024.0);        
    >;
    
    parameter float angle
    <
        defaultValue : float(1.57);
        minValue : float(0.0);
        maxValue : float(6.3);        
    >;
    
    void
    evaluatePixel()
    {
        float2 posC=outCoord()-center;
        float rr=sqrt(posC.x*posC.x+posC.y*posC.y);
        float aa= atan(posC.y, posC.x) + angle * (radius - rr) / radius;
        if (rr<radius)
        dst=sampleNearest(src,float2(center.x+rr*cos(aa),center.y+rr*sin(aa)));
        else
        dst=sampleNearest(src,outCoord());
    }
}
